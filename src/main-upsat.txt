//main-upsat.txt

#include <Adafruit_LSM9DS1.h>
#include "upsat/WahbaRotM.hpp"

// constants
float conversionRadiansToDegrees = 180.0/M_PI;
float loopFreq = 100;
float loopPeriod = 1 / loopFreq;

// LSM9DS1 object and upsat algorithm structs
Adafruit_LSM9DS1 lsm = Adafruit_LSM9DS1();
sensorDat sensorData;
WahbaRotMStruct WStruct;

// set up sensors range/sensitivity
void setupSensor() {
  lsm.setupAccel(lsm.LSM9DS1_ACCELRANGE_2G);
  lsm.setupMag(lsm.LSM9DS1_MAGGAIN_4GAUSS);
  lsm.setupGyro(lsm.LSM9DS1_GYROSCALE_245DPS);
}

void setup() {
  Serial.begin(9600);
  while(!Serial) {delay(1);} // will pause Zero, Leonardo, etc until serial console opens
  
  // Try to initialise and warn if we couldn't detect the chip
  if(!lsm.begin()) {
    Serial.println("Oops ... unable to initialize the LSM9DS1. Check your wiring!");
    while (1);
  }
  Serial.println("Found LSM9DS1 9DOF");

  // set up sensors and upsat algorithm main struct
  setupSensor();
  initWahbaStruct(&WStruct, loopPeriod);
}

void loop() {
  // get data from sensors
  sensors_event_t accelerometers, magnetometers, gyroscopes, temperature;
  lsm.getEvent(&accelerometers, &magnetometers, &gyroscopes, &temperature);

  // create vectors with data
  float accelsRawXYZ[3] = {accelerometers.acceleration.x, accelerometers.acceleration.y, accelerometers.acceleration.z};
  float gyrosRawXYX[3] = {gyroscopes.gyro.x, gyroscopes.gyro.y, gyroscopes.gyro.z};
  float magsRawXYZ[3] = {magnetometers.magnetic.x, magnetometers.magnetic.y, magnetometers.magnetic.z};

  // raw sensor data
  String rawAccelerometersStr = String(accelerometers.acceleration.x) + ",  " + String(accelerometers.acceleration.y) + ",  " + String(accelerometers.acceleration.z);
  String rawGyroscopesStr = String(gyroscopes.gyro.x) + ",  " + String(gyroscopes.gyro.y) + ",  " + String(gyroscopes.gyro.z);
  String rawMagnetometersStr = String(magnetometers.magnetic.x) + ",  " + String(magnetometers.magnetic.y) + ",  " + String(magnetometers.magnetic.z);
  String rawDataStr = rawAccelerometersStr + ",  " + rawGyroscopesStr + ",  " + rawMagnetometersStr;


  // calculate attitude using upsat algorithm
  sensorDat sensorData = scaleSens(accelsRawXYZ, gyrosRawXYX, magsRawXYZ);
  WahbaRotM(sensorData.Acc, sensorData.Gyr, sensorData.Mag, &WStruct);
  
  // print pith, roll and yaw
  String attitudeDataStr = String(WStruct.Euler[0] * conversionRadiansToDegrees) + ", " + String(WStruct.Euler[1] * conversionRadiansToDegrees) + ",  " + String(WStruct.Euler[2] * conversionRadiansToDegrees);
  String dataStr = rawDataStr + ", " + attitudeDataStr;
  Serial.println(dataStr);
} 